#!/usr/bin/env perl
use strict;
use warnings;

use lib "lib";
use Scalar::Util qw/blessed/;
local $SIG{__DIE__} = sub {
	my $err = shift;
	print STDERR blessed $err ? $err->{parameter} : $err;
};
use ENClient;
use App::Rad;
use File::HomeDir;
use HTML::Entities;
use HTML::Escape qw/escape_html/;
use MIME::Types;
use ENResource;

sub setup {
	my $c = shift;
	my $home = File::HomeDir->my_home;
	$c->register(run 	=> \&run);
	$c->register(text 	=> \&text);
	$c->register(man 	=> \&man);
	$c->register(file 	=> \&file);
	$c->register(search 	=> \&search);
	$c->register(show 	=> \&show);

	$c->load_config("$home/.evernoteclirc");
	die "Please, get a auth token on https://sandbox.evernote.com/api/DeveloperToken.action (if you want to test on a sandbox) and fill the auth_token field on your conf file." unless exists $c->config->{auth_token};
	$c->stash->{auth_token} = $c->config->{auth_token};
}

sub post_process {
	my $c = shift;
	if(blessed($c->output) and $c->output->isa("EDAMNoteStore::NoteMetadata")) {
		print Dumper $c->output
	} elsif(blessed($c->output)) {
		print $c->_success_msg($c->output)
	} elsif(ref $c->output eq "ARRAY") {
		print "\033[1m";
		printf " %-30s     %s$/$/", "Title", "GUID";
		print "\033[m";
		printf " %-30s  -  %s$/", $_->title, $_->guid for @{ $c->output }
	} else {
		print $c->output, $/
	}
}

sub run :Help(Send the return of a command to evernote) {
	my $c = shift;
	my $command = join " ", @{ $c->argv };
	my $return  = qx/$command/;
	$c->_create_note($c->stash->{auth_token}, $command, $return)
}

sub man :Help(Send the man page to evernote) {
	my $c = shift;
	my $command = shift @{ $c->argv };
	my $return  = qx/man -P cat $command/;
	$c->_create_note($c->stash->{auth_token}, "man $command", $return)
}

sub text :Help(Send the stdio to evernote) {
	my $c = shift;
	my $title = join " ", @{ $c->argv };
	@ARGV = ();
	my $body;
	while(my $line = <>) {
		$body .= $line;
	}
	$c->_create_note($c->stash->{auth_token}, $title, $body)
}

sub file :Help(Send files to evernote) {
	my $c     = shift;
	my @files = @{ $c->argv };

	$c->_create_note($c->stash->{auth_token}, join(", ", @files), "", @files)
}

sub search :Help(Search on evernote) {
	my $c          = shift;
	my $search_str = join " ", @{ $c->argv };

	[ $c->_search($c->stash->{auth_token}, $search_str) ]
}

sub show :Help(Show a note from evernote by GUID) {
	my $c    = shift;
	my $guid = shift;

	$c->_get_note($c->stash->{auth_token}, $guid)
}

*App::Rad::_success_msg = sub {
	my $c    = shift;
	my $note = shift;
	sprintf "Successfully created a new note with GUID: %s\n", $note->guid;
};

*App::Rad::_get_note = sub {
	my $c          = shift;
	my $auth_token = shift;
	my $guid       = shift;

	my $enclient = ENClient->new;
	die "Late version..." if not $enclient->check_version;

	$enclient->auth_token($auth_token);

	$enclient->get_note($guid)
};

*App::Rad::_search = sub {
	my $c          = shift;
	my $auth_token = shift;
	my $search_str = shift;

	my $enclient = ENClient->new;
	die "Late version..." if not $enclient->check_version;

	$enclient->auth_token($auth_token);

	$enclient->search($search_str)
};

*App::Rad::_create_note = sub {
	my $c = shift;
	my $auth_token = shift;
	my $title = shift;
	my $body  = decode_entities(shift);
	my @files = @_;

	$body =~ s/\x{008}//g;
	$body = escape_html($body);
	my $enclient = ENClient->new;
	die "Late version..." if not $enclient->check_version;

	$enclient->auth_token($auth_token);
	
	my $note = EDAMTypes::Note->new();
	
	$note->title( exists($c->options->{title}) ? $c->options->{title} :  $title );
	
	my $content = <<"FIM";
<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE en-note SYSTEM "http://xml.evernote.com/pub/enml2.dtd">
<en-note>
FIM
	if($body) {
		$content .= <<"FIM";
<pre style="background-color: black; color: green; font-family: 'Courier New', Courier, monospace">
$body
</pre>
FIM
	}

	my @resources;
	my $mime;
	if(@files) {
		$mime = MIME::Types->new;
	}
	for my $filename(@files) {
		my $resource = ENResource->new($filename);
		push @resources, $resource->resource;
		my $mimetype = $resource->mime;
		my $hash_hex = $resource->md5_hex;
		$content .= qq{<div><en-media type="$mimetype" hash="$hash_hex"/></div>};
	}
	$content .= "</en-note>";
	$note->resources(\@resources);
	$note->content($content);
	$enclient->note_store->createNote( $enclient->auth_token, $note );
};

App::Rad->run;
